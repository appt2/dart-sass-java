plugins {
    id "com.google.protobuf" version "0.9.4"
    id "io.freefair.maven-publish-java"
    id "idea"
    id "java-library"
}

description = "SASS Embedded Protocol"

publishing.publications.mavenJava.pom.description = "Java DTOs for the embedded-sass-protocol"

repositories {

}

def protobufVersion = "3.25.3"

tasks.withType(com.google.protobuf.gradle.ProtobufExtract).configureEach {
    doFirst {
        project.delete destDir
    }
}

dependencies {
    protobuf "sass:embedded-protocol:$embeddedProtocolVersion@zip"

    api "com.google.protobuf:protobuf-java:$protobufVersion"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
}

tasks.named("jar", Jar) {
    manifest {
        attributes 'Specification-Vendor': "Google LLC",
                'Specification-Version': "$embeddedProtocolVersion"
    }
}
