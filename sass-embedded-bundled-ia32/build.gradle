plugins {
    id "java-library"
    id "io.freefair.maven-publish-java"
}

description = "SASS Embedded Bundled Compilers ia32"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    sassEmbedded
}

repositories {
    ivy {
        //https://github.com/sass/dart-sass/releases/download/1.63.3/dart-sass-1.63.3-linux-x64.tar.gz
        url "https://github.com"
        patternLayout {
            artifact "/[orgPath]/[artifact]/releases/download/[revision]/[artifact]-[revision](-[classifier])(.[ext])"
        }
        metadataSources {
            artifact()
        }
        content {
            includeModule("sass", "dart-sass")
        }
    }
}

dependencies {
    sassEmbedded "sass:dart-sass:$dartSassVersion:linux-ia32@tar.gz"

    sassEmbedded "sass:dart-sass:$dartSassVersion:linux-ia32-musl@tar.gz"

    sassEmbedded "sass:dart-sass:$dartSassVersion:windows-ia32@zip"
}

tasks.named("processResources") {
    into("de/larsgrefer/sass/embedded/bundled") {
        from configurations.sassEmbedded
        rename "-$dartSassVersion-", "-"
    }
}

tasks.named("jar") {
    manifest {
        attributes 'Specification-Vendor': "Google LLC",
                'Specification-Version': "$dartSassVersion"
    }
}