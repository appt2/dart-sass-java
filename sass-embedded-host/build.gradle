plugins {
    id "java-library"
    id "io.freefair.maven-publish-java"
    id "jacoco"
}

description = "SASS Embedded Host"

configurations {
    sassEmbedded
}

javadoc.failOnError = false

repositories {
    ivy {
        //https://github.com/sass/dart-sass-embedded/releases/download/$dartSassVersion/sass_embedded-$dartSassVersion-linux-ia32.tar.gz
        url "https://github.com/sass/dart-sass-embedded/releases/download"
        patternLayout {
            artifact "/[revision]/[artifact]-[revision](-[classifier])(.[ext])"
        }
        metadataSources {
            artifact()
        }
    }
}

def dartSassVersion = "1.0.0-beta.16"
def slf4jVersion = '1.7.33'
def springVersion = '5.3.15'
def webjarsVersion = '0.42'

dependencies {
    api project(":sass-embedded-protocol")
    api "org.slf4j:slf4j-api:$slf4jVersion"

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly "org.springframework:spring-core:$springVersion"
    compileOnly "org.webjars:webjars-locator:$webjarsVersion"

    sassEmbedded "sass:sass_embedded:$dartSassVersion:linux-ia32@tar.gz"
    sassEmbedded "sass:sass_embedded:$dartSassVersion:linux-x64@tar.gz"
    sassEmbedded "sass:sass_embedded:$dartSassVersion:macos-x64@tar.gz"
    sassEmbedded "sass:sass_embedded:$dartSassVersion:windows-ia32@zip"
    sassEmbedded "sass:sass_embedded:$dartSassVersion:windows-x64@zip"

    testImplementation "org.springframework:spring-core:$springVersion"
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
    testRuntimeOnly "org.webjars:webjars-locator:$webjarsVersion"
    testRuntimeOnly 'org.webjars:bootstrap:5.1.1'
}

processResources {
    into("de/larsgrefer/sass/embedded") {
        from configurations.sassEmbedded
        rename "-$dartSassVersion-", "-"
    }
}

jar.manifest {
    attributes 'Specification-Version': "$dartSassVersion"
}
